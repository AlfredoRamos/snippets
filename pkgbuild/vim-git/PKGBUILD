# $Id$
# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: tobias [ tobias at archlinux org ]
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=vim
pkgname=('vim-git' 'vim-runtime-git')
pkgver=9.0.2081.0.g1bf1bf569
pkgrel=1
arch=('i686' 'x86_64' 'x86_64_v3' 'armv7h' 'aarch64')
license=('custom:vim')
url='http://www.vim.org'
makedepends=('glibc' 'libgcrypt' 'zlib' 'gpm' 'python' 'ruby' 'libxt' 'lua' 'gawk' 'tcl' 'git')
source=(
	'git+https://github.com/vim/vim.git'
	'vimrc'
	'archlinux.vim'
	'vimdoc.hook'
)
sha512sums=(
	'SKIP'
	'4b5bed0813f22af9e158ea9aa56a4a9862dd786ba2d201f20159ccf652da6190164aaed0b6b7217d578f7b25c33a8adcc307bfcf3caa8d173a7ff29e2a00fee7'
	'fe091d289d876f45319c898f6021ef86d6a238b540c225a279c46efc5c36fa7d868cd0cee73a111811c4be90df160f85340bb251be3a437727dbe5c699950363'
	'a02ad0d66f300160911aeb81d8886c6c558436ac4ee3fcd161dd65c6b1e5d1f41b9005a7f5bb5ba68d57027fc1c8e43daabf055bd6207fb5a216a67f758df8d1'
)

pkgver() {
	# Updating package version
	cd "${srcdir}"/${pkgbase}
	git describe --long --tags | sed -r 's/^v//;s/-/./g'
}

prepare() {
	# Create build directory
	mkdir -p "${srcdir}"/build
	cp -a "${srcdir}"/${pkgbase}/* "${srcdir}"/build

	cd "${srcdir}"/build/src

	# define the place for the global (g)vimrc file (set to /etc/vimrc)
	sed -E 's|^.*(#define SYS_.*VIMRC_FILE.*").*$|\1|g' -i feature.h
	sed -E 's|^.*(#define VIMRC_FILE.*").*$|\1|g' -i feature.h
	autoconf
}

build() {
	cd "${srcdir}"/build

	./configure \
		--prefix=/usr \
		--localstatedir=/var/lib/vim \
		--with-features=huge \
		--with-compiledby='Arch Linux' \
		--enable-gpm \
		--enable-acl \
		--with-x=yes \
		--disable-gui \
		--enable-multibyte \
		--enable-cscope \
		--enable-netbeans \
		--enable-perlinterp=dynamic \
		--enable-python3interp=dynamic \
		--enable-rubyinterp=dynamic \
		--enable-luainterp=dynamic \
		--enable-tclinterp=dynamic \
		--disable-canberra
	make
}

# check() {
# 	cd "${srcdir}"/build
# 	TERM=xterm make -j1 test
# }

package_vim-git() {
	pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor. Development version.'
	depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'acl' 'glibc' 'libgcrypt' 'zlib')
	optdepends=(
		'python: Python 3 language support'
		'ruby: Ruby language support'
		'lua: Lua language support'
		'perl: Perl language support'
		'tcl: Tcl language support'
	)
	conflicts=('gvim' 'vim-minimal' 'vim-python3')
	provides=(
		"vim-minimal=${pkgver}-${pkgrel}"
		"vim-plugin-runtime=${pkgver}-${pkgrel}"
		"xxd=${pkgver}-${pkgrel}"
	)
	replaces=('vim-python3' 'vim-minimal')

	cd "${srcdir}"/build
	make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

	# provided by (n)vi in core
	rm "${pkgdir}"/usr/bin/{ex,view}

	# delete some manpages
	find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
	while read _mandir; do
		cd "${_mandir}"
		rm -f ex.1 view.1 # provided by (n)vi
		rm -f evim.1    # this does not make sense if we have no GUI
	done

	# Runtime provided by runtime package
	rm -r "${pkgdir}"/usr/share/vim

	# no desktop files and icons
	rm -r "${pkgdir}"/usr/share/{applications,icons}

	# license
	install -Dm644 runtime/doc/uganda.txt \
		"${pkgdir}"/usr/share/licenses/${pkgname}/license.txt

	# pacman hook for documentation helptags
	install -Dm 644 "${srcdir}"/vimdoc.hook "${pkgdir}"/usr/share/libalpm/hooks/vimdoc.hook
}

package_vim-runtime-git() {
	pkgdesc='Runtime for vim and gvim. Development version.'
	conflicts=('vim-runtime')
	provides=("vim-runtime=${pkgver}-${pkgrel}")
	backup=('etc/vimrc')

	cd "${srcdir}"/build
	local versiondir=$(printf "${pkgver}" | sed -r 's/^([0-9]+)\.([0-9]+)(.*)/\1\2/')

	make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install
	# man and bin files belong to 'vim'
	rm -r "${pkgdir}"/usr/share/man/ "${pkgdir}"/usr/bin/

	# Don't forget logtalk.dict
	install -Dm644 runtime/ftplugin/logtalk.dict \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/ftplugin/logtalk.dict

	# rc files
	install -Dm644 "${srcdir}"/vimrc "${pkgdir}"/etc/vimrc
	install -Dm644 "${srcdir}"/archlinux.vim \
		"${pkgdir}"/usr/share/vim/vimfiles/archlinux.vim

	# no desktop files and icons
	rm -r "${pkgdir}"/usr/share/{applications,icons}

	# license
	install -dm755 "${pkgdir}"/usr/share/licenses/vim-runtime
	ln -s /usr/share/vim/vim${versiondir}/doc/uganda.txt \
		"${pkgdir}"/usr/share/licenses/vim-runtime/license.txt
}
