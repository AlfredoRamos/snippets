# $Id$
# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: tobias [ tobias at archlinux org ]
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=vim
pkgname=('vim-git' 'vim-runtime-git')
pkgver=8.1.0884.0.gaf630d4f7
pkgrel=1
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
license=('custom:vim')
url='http://www.vim.org'
makedepends=('gpm' 'python2' 'python' 'ruby' 'libxt' 'lua' 'gawk' 'tcl')
source=(
	'git+https://github.com/vim/vim.git'
	'vimrc'
	'archlinux.vim'
	'vimdoc.hook'
)
sha512sums=(
	'SKIP'
	'4b5bed0813f22af9e158ea9aa56a4a9862dd786ba2d201f20159ccf652da6190164aaed0b6b7217d578f7b25c33a8adcc307bfcf3caa8d173a7ff29e2a00fee7'
	'fe091d289d876f45319c898f6021ef86d6a238b540c225a279c46efc5c36fa7d868cd0cee73a111811c4be90df160f85340bb251be3a437727dbe5c699950363'
	'1e06e981691b17662fd0fddac5c00c87c920d1b4a1cbb6191c42d57cc40b00af12710e26b22fcfc0901bb8142b15f6a04aa65cec2d9b3bb9d5a06cb650d3ab9c'
)

pkgver() {
	# Updating package version
	cd "${srcdir}"/${pkgbase}
	git describe --long --tags | sed -r 's/^v//;s/-/./g'
}

prepare() {
	# Create build directory
	mkdir -p "${srcdir}"/build
	cp -a "${srcdir}"/${pkgbase}/* "${srcdir}"/build

	cd "${srcdir}"/build/src

	# define the place for the global (g)vimrc file (set to /etc/vimrc)
	sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' feature.h
	sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' feature.h
	autoconf
}

build() {
	cd "${srcdir}"/build

	./configure \
		--prefix=/usr \
		--localstatedir=/var/lib/vim \
		--with-features=huge \
		--with-compiledby='Arch Linux' \
		--enable-gpm \
		--enable-acl \
		--with-x=yes \
		--disable-gui \
		--enable-multibyte \
		--enable-cscope \
		--enable-netbeans \
		--enable-perlinterp=dynamic \
		--enable-pythoninterp=dynamic \
		--enable-python3interp=dynamic \
		--enable-rubyinterp=dynamic \
		--enable-luainterp=dynamic \
		--enable-tclinterp=dynamic
	make
}

check() {
	cd "${srcdir}"/build
	#TERM=xterm make -j1 test
}

package_vim-git() {
	pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor. Development version.'
	depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'acl')
	optdepends=(
		'python2: Python 2 language support'
		'python: Python 3 language support'
		'ruby: Ruby language support'
		'lua: Lua language support'
		'perl: Perl language support'
		'tcl: Tcl language support'
	)
	conflicts=('gvim' 'vim-minimal' 'vim-python3')
	provides=("vim=${pkgver}-${pkgrel}" "vim-python3=${pkgver}-${pkgrel}" "xxd=${pkgver}-${pkgrel}")
	replaces=('vim-python3' 'vim-minimal')

	cd "${srcdir}"/build
	make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

	# provided by (n)vi in core
	rm "${pkgdir}"/usr/bin/{ex,view}

	# delete some manpages
	find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
	while read _mandir; do
		cd "${_mandir}"
		rm -f ex.1 view.1 # provided by (n)vi
		rm -f evim.1    # this does not make sense if we have no GUI
	done

	# Runtime provided by runtime package
	rm -r "${pkgdir}"/usr/share/vim

	# no desktop files and icons
	rm -r "${pkgdir}"/usr/share/{applications,icons}

	# license
	install -Dm644 runtime/doc/uganda.txt \
		"${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}

package_vim-runtime-git() {
	pkgdesc='Runtime for vim and gvim. Development version.'
	conflicts=('vim-runtime')
	provides=("vim-runtime=${pkgver}-${pkgrel}")
	backup=('etc/vimrc')

	cd "${srcdir}"/build
	local versiondir=$(printf "${pkgver}" | sed -r 's/^([0-9]+)\.([0-9]+)(.*)/\1\2/')

	make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install
	# man and bin files belong to 'vim'
	rm -r "${pkgdir}"/usr/share/man/ "${pkgdir}"/usr/bin/

	# Don't forget logtalk.dict
	install -Dm644 runtime/ftplugin/logtalk.dict \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/ftplugin/logtalk.dict

	# fix FS#17216
	sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/filetype.vim

	# patch filetype.vim for better handling of pacman related files
	sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/filetype.vim
	sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/ftplugin/changelog.vim

	# rc files
	install -Dm644 "${srcdir}"/vimrc "${pkgdir}"/etc/vimrc
	install -Dm644 "${srcdir}"/archlinux.vim \
		"${pkgdir}"/usr/share/vim/vimfiles/archlinux.vim

	# rgb.txt file
	install -Dm644 runtime/rgb.txt \
		"${pkgdir}"/usr/share/vim/vim${versiondir}/rgb.txt

	# no desktop files and icons
	rm -r "${pkgdir}"/usr/share/{applications,icons}

	# license
	install -dm755 "${pkgdir}"/usr/share/licenses/vim-runtime
	ln -s /usr/share/vim/vim${versiondir}/doc/uganda.txt \
		"${pkgdir}"/usr/share/licenses/vim-runtime/license.txt

	# pacman hook for documentation helptags
	install -Dm644 "${srcdir}"/vimdoc.hook "${pkgdir}"/usr/share/libalpm/hooks/vimdoc.hook
}
